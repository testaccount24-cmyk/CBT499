         MACRO                            +
&CNAME   ENTER &SAVE=SAVE,&BASE=(12),&EQU=YES,&CSECT=YES,&RENT=NO,     +
               &SP=0,&AUTH=,&FUNC=,&DATEW=,&PGM=,&TRACE=OFF,&BLOCK=NO, +
               &SA=YES
         GBLB  &FTIME,&GETMAIN,&TRACEP,&SAC
         GBLA  &SUBP
         LCLA  &NUMBAS,&SUP,&FUNCE        +
         LCLC  &NAMSAV,&USEBASE,&FUNC1,&FUNC2
         LCLC  &PGM1                      +
         PRINT ON                         +
&SAC     SETB  ('&SA' EQ 'YES')           +
&GETMAIN SETB  ('&RENT' EQ 'YES')         +
&SUBP    SETA  &SP                        +
&TRACEP  SETB  ('&TRACE' EQ 'ON')         +
         AIF   ('&CNAME' NE '').E10       +
         MNOTE 8,'NO CSECT NAME SPECIFIED'
         MEXIT                            +
.E10     AIF   (K'&SAVE LT 5).E20         +
         MNOTE 8,'SAVE AREA MORE THAN 4 CHARACTERS'
         MEXIT                            +
.E20     ANOP                             +
         AIF   ('&BLOCK' EQ 'NO').NOBLK   +DONT GENERATE HEADING BLOCK
&PGM1    SETC  '&PGM'                     +SET IT TO CSECT NAME IF NULL
         AIF   ('&PGM' NE '').GENHD       +GO GEN HEADINGS
&PGM1    SETC  '&CNAME'                   +SET IT TO CSECT NAME IF NULL
.GENHD   ANOP                             +
         AIF   ('&FUNC' EQ '').GENBLK     +NO FUNCTION SPECIFIED
         AIF   ('&FUNC'(1,1) EQ '''').Q   +FUNC IN QUOTES ????
&FUNC1   SETC  ''''                       +MAKE IT A QUAOTE
&FUNC1   SETC  '&FUNC1.&FUNC.&FUNC1'      +FORCE QUOTES
         AGO   .Q1                        +
.Q       ANOP                             +
&FUNC1   SETC  '&FUNC'                    +
.Q1      ANOP                             +
&FUNCE   SETA  44                         +SET END OF FUNC
         AIF   (K'&FUNC1 GT 45).GENTIT    +CHECK FUNC LEN
&FUNCE   SETA  (K'&FUNC1-2)               +IF LT 45 SET RIGHT
.GENTIT  ANOP                             +
&FUNC2   SETC  '&FUNC1'(2,&FUNCE)         +EXTRACT FROM FUNC FOR TITLE
         TITLE '&PGM1 - &FUNC2'           +
.GENBLK  ANOP                             +
         PSHD1 PGM=&PGM1,FUNC=&FUNC1,AUTH=&AUTH,DATEW=&DATEW +GEN HEAD
.NOBLK   ANOP                             +
&NAMSAV  SETC  '&SAVE'.'&SYSNDX'          +
         AIF   ('&BASE'(1,1) EQ '(').E22  +
&USEBASE SETC  '&BASE'                    +
         AGO   .E24                       +
.E22     ANOP                             +
&NUMBAS  SETA  K'&BASE-2                  +
&USEBASE SETC  '&BASE'(2,&NUMBAS)         +
.E24     ANOP                             +
&NUMBAS  SETA  1                          +
         AIF   ('&CSECT' EQ 'NO').E25     +
&CNAME   CSECT                            +
.E25     ANOP                             +
         USING *,&USEBASE                 +
         PSPGMID1 &CNAME                  +SET UP ID
         STM   14,12,12(13)               +
         LR    &BASE(1),15                +SET UP FIRST BASE REG
         AIF   (NOT &SAC).BASES           +NO SAVE AREA CHAINING
         LR    14,13                      +STORE SAVE AREA POINTER
         AIF   (NOT &GETMAIN).E26         +
&SUP     SETA  &SP                        +
         AIF   (NOT &SUP).E26A            +IS SUBPOOL ?
         AGO   .E26B                      +YES
.E26A    GETMAIN R,LV=72                  +GET SAVE AREA
         LR    13,1                       +ADDRESS OF SAVE AREA
         AGO   .E27                       +
.E26B    ANOP                             +
         GETMAIN RC,LV=72,SP=&SUP         +SAVE AREA WITH SUBPOOL
         LR    13,1                       +
         AGO   .E27                       +
.E26     ANOP                             +
         LA    13,&NAMSAV                 +LOAD ADDRESS OF NEW S.A.
.E27     ANOP                             +
         ST    13,8(14)                   +SAVE AREA FORWARD CHAIN
         ST    14,4(13)                   +SAVE AREA BACKWARD CHAIN
         LM    14,1,12(14)                +RESTORE REGS 14-1
.BASES   ANOP
         AIF   (N'&BASE EQ &NUMBAS).E40   +
         LA    15,4095                    +X'1000' - 1
.E30     AIF   (N'&BASE EQ &NUMBAS).E40   +
&NUMBAS  SETA  &NUMBAS+1                  +
         LA    &BASE(&NUMBAS),1(15,&BASE(&NUMBAS-1)) +SECONDARY BASE
         AGO   .E30                       +
.E40     ANOP                             +
         AIF   (&GETMAIN).E45             +EXTERNAL SA
         AIF   (NOT &SAC).E45             +NO SA
         B     $$EN&SYSNDX                +BRANCH AROUND SAVE AREA
&NAMSAV  DC    9D'0'                      +DEFINE SAVE AREA
.E45     ANOP                             +
         AIF   ('&EQU' EQ 'NO').E50       +
         AIF   (&FTIME).E50               +
&FTIME   SETB  1                          +
R0       EQU   0                          +
R1       EQU   1                          +
R2       EQU   2                          +
R3       EQU   3                          +
R4       EQU   4                          +
R5       EQU   5                          +
R6       EQU   6                          +
R7       EQU   7                          +
R8       EQU   8                          +
R9       EQU   9                          +
RA       EQU   10                         +
RB       EQU   11                         +
RC       EQU   12                         +
RD       EQU   13                         +
RE       EQU   14                         +
RF       EQU   15                         +
R10      EQU   10                         +
R11      EQU   11                         +
R12      EQU   12                         +
R13      EQU   13                         +
R14      EQU   14                         +
R15      EQU   15                         +
FPR0     EQU   0                          +
FPR2     EQU   2                          +
FPR4     EQU   4                          +
FPR6     EQU   6                          +
.E50     ANOP                             +
         AIF   (&GETMAIN).E60             +
&USEBASE SETC  '$$EN'.'&SYSNDX'           +
&USEBASE EQU   *                          +
.E60     ANOP                             +
         MEND                             +
