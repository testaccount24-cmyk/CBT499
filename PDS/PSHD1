         MACRO
&NAME    PSHD1 &PGM=,&AUTH=,&DATEW=,&TYPE=1,&FUNC=
.*******       ********************************************************
.*******       *PSHD1    GENERATE PROGRAM HEADING BLOCK               *
.*******       ********************************************************
         LCLC  &HYPH,&ADATE,&WDATE          +ASSEMBLY DATE,DATE WRITTEN
         LCLC  &AYY,&AMM,&ADD               +ASSEMBLY DATE
         LCLC  &FUNC1,&FUNC2,&FUNC3         +FUNCTION DESCRIPTION
         LCLC  &WYYC,&WMMC,&WDDC            +DATE WRITTEN
         LCLA  &WYY,&WMM,&WDD               +DATE WRITTEN
         LCLC  &ERMSG                       +ERROR MESSAGE
&HYPH    SETC  '-'                          +SET HYPHEN
         MNOTE '********       ****************************************+
               ****************'            +WRITE FIRST LINE
.*******       ********************************************************
.*******       *CHECK IF REQUIRED PARMS ARE PRESENT *******************
.*******       ********************************************************
&ERMSG   SETC  'PGM'                        +INSERT PARM TYPE
         AIF   (K'&PGM EQ 0).PARMERR        +CHECK PARM LENGTH
&ERMSG   SETC  'AUTH'                       +INSERT PARM TYPE
         AIF   (K'&AUTH EQ 0).PARMERR       +CHECK PARM LENGTH
&ERMSG   SETC  'DATEW'                      +INSERT PARM TYPE
         AIF   (K'&DATEW EQ 0).PARMERR      +CHECK PARM LENGTH
&ERMSG   SETC  'FUNC'                       +INSERT PARM TYPE
         AIF   (K'&FUNC EQ 0).PARMERR       +CHECK PARM LENGTH
         AGO   .GENBLK                      +GO GENERATE BLK CHARS
.PARMERR ANOP                               +PARM MISSING OR TOO SHORT
         MNOTE  3,'PARAMETER MISSING OR TOO SHORT &ERMSG'
.*******       ********************************************************
.*******       *PRINT PROGRAM NAME IN BLOCK CHARACTERS ****************
.*******       ********************************************************
.GENBLK  ANOP                               +PARM MISSING OR TOO SHORT
         PSBC0 PROGRAM=&PGM,TYPE=1          +PRINT BLK CHARS
         MNOTE '********       ****************************************+
               ****************'            +WRITE END   LINE
.*******       ********************************************************
.*******       *SET UP ASSEMBLY DATE **********************************
.*******       ********************************************************
&ADATE   SETC  '&SYSDATE'                   +GET DATE
&AYY     SETC  '&ADATE'(7,2)                +EXTRACT YEAR
&AMM     SETC  '&ADATE'(1,2)                +EXTRACT MONTH
&ADD     SETC  '&ADATE'(4,2)                +EXTRACT DAY
&ADATE   SETC  '&AYY.&HYPH.&AMM.&HYPH.&ADD' +BUILD DATE
.*******       ********************************************************
.*******       *SET UP DATE WRITTEN  **********************************
.*******       ********************************************************
         AIF   (K'&DATEW LE 6).DWLOK        +CHECK LENGTH
         MNOTE 4,'DATE WRITTEN &DATEW LONGER THAN 6 DIGITS -TRUNCATED'
.DWLOK   ANOP                               +
&WYY     SETA   &DATEW/10000                +EXTRACT YEAR
&WMM     SETA   (&DATEW/100)-(&WYY*100)     +EXTRACT MONTH
&WDD     SETA   &DATEW-((&WYY*10000)+(&WMM*100)) +GET DAY
         AIF   (&WYY LE &AYY AND &WYY GE 64).DWYOK +CHECK YEAR
         MNOTE 1,'YEAR WRITTEN NOT IN RANGE 64 TO &AYY'
.DWYOK   ANOP                               +
         AIF   (&WMM GE 1 AND &WMM LE 12).DWMOK   +CHECK MONTH
         MNOTE 1,'MONTH WRITTEN NOT IN RANGE 1 TO 12'
.DWMOK   ANOP                               +
         AIF   (&WDD GE 1 AND &WDD LE 31).DWDOK   +CHECK DAY
         MNOTE 1,'DAY WRITTEN NOT IN RANGE 1 TO 31'
.DWDOK   ANOP                               +
         AIF   (&WDD LT 10).ADD0D           +ADD A 0
&WDDC    SETC  '&WDD'
         AGO   .CHKM
.ADD0D   ANOP
&WDDC    SETC  '0&WDD'
.CHKM    ANOP
         AIF   (&WMM LT 10).ADD0M           +ADD A 0
&WMMC    SETC  '&WMM'
         AGO   .SETYY
.ADD0M   ANOP
&WMMC    SETC  '0&WMM'
.SETYY   ANOP
&WYYC    SETC  '&WYY'
&WDATE   SETC  '&WYYC.&HYPH.&WMMC.&HYPH.&WDDC' +BUILD DATE
.*******       ********************************************************
.*******       *SET UP FUNCTION ***************************************
.*******       ********************************************************
         AIF   (K'&FUNC LE 116).LFOK1       +IN MAX RANGE
         MNOTE 1,'FUNCTION LONGER THAN 114 CHARACTERS - TRUNCATED'
.LFOK1   ANOP                               +LENGTH LE 114
         AIF   (K'&FUNC GT 40).LFUNC1       +CHECK FUNC LENGTH
&FUNC1   SETC  '&FUNC'(2,K'&FUNC-2)         +DROP QUOTES
         MNOTE '********       *FUNCTION.......:&FUNC1'
         AGO   .ENDFUNC
.LFUNC1  ANOP                               +LENGTH LE 114
         AIF   (K'&FUNC GT 78).LFUNC2       +CHECK FUNC LENGTH
&FUNC1   SETC  '&FUNC'(2,38)
         MNOTE '********       *FUNCTION.......:&FUNC1'
&FUNC2   SETC  '&FUNC'(40,K'&FUNC-40)
         MNOTE '********       *...............:&FUNC2'
         AGO   .ENDFUNC
.LFUNC2  ANOP                               +LENGTH GT 76
&FUNC1   SETC  '&FUNC'(2,38)
         MNOTE '********       *FUNCTION.......:&FUNC1'
&FUNC2   SETC  '&FUNC'(40,38)
         MNOTE '********       *...............:&FUNC2'
         AIF   (K'&FUNC LE 116).LFUNC3      +IN MAX RANGE
&FUNC3   SETC  '&FUNC'(78,115-78)
         AGO   .LFUNC4
.LFUNC3  ANOP                               +LENGTH GT 76
&FUNC3   SETC  '&FUNC'(78,K'&FUNC-78)
.LFUNC4  ANOP                               +LENGTH GT 76
         MNOTE '********       *...............:&FUNC3'
.ENDFUNC ANOP
.*******       ********************************************************
.*******       *PRINT REST OUT ****************************************
.*******       ********************************************************
.*******       *MAXIMUM PARM LENGTH = 38
         MNOTE '********       *DATE WRITTEN...:&WDATE'
         MNOTE '********       *DATE ASSEMBLED.:&ADATE'
         MNOTE '********       *AUTHOR.........:&AUTH'
         MNOTE '********       ****************************************+
               ****************'
         EJECT
         MEND                           +
