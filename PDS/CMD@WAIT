CMD@WAIT CSECT
CMD@WAIT AMODE 24
CMD@WAIT RMODE 24
**********************************************************************
** FUNCTION: THIS PROGRAM ISSUES MULTIPLE SYSTEM COMMANDS           **
**                                                                  **
** INPUT:  COMMANDS TO BE ISSUED READ FROM SYSIN.                   **
**         TIME TO WAIT BETWEEN COMMANDS - FROM INPUT PARM,         **
**         DEFAULT IS 1 SECOND.                                     **
**                                                                  **
** OUTPUT: SYSTEM COMMANDS                                          **
**                                                                  **
** ATTRIBUTES: RE-ENTRANT                                           **
**             THE PROGRAM SHOULD BE APF AUTHORIZED AND LINKED AC=1 **
**                                                                  **
**********************************************************************
         STM   R14,R12,12(R13)          .SAVE REGISTERS
         LR    R12,R15                  .LOAD BASE ADDRESS
         USING CMD@WAIT,R12             .SET BASE REGISTER
         L     R2,0(,R1)                .SAVE PARAMETER POINTER
*
         LA    R10,LENWORK              .LOAD LENGTH OF WORKSTOR
         GETMAIN R,LV=(10)              .GET MAIN STORAGE - WORKSTOR
         LR    R11,R1                   .LOAD ADDRESS     - WORKSTOR
         USING WORKSTOR,R11             .SET BASE ADDRESS - WORKSTOR
         ST    R13,4(,R11)              .SAVE AREA POINTER
         ST    R11,8(,R13)              .SAVE AREA POINTER
         LR    R13,R11                  .SAVE AREA ADDRESS IN R13
*
**********************************************************************
*** PROCESS INPUT WAIT TIME
**********************************************************************
         MVC   WTIME,=C'00000100'      .INITIALIZE WAIT TIME
         LA    R2,0(,R2)               .GET RID OF HIGH ORDER BIT
         LH    R9,0(,R2)               .LOAD LENGTH OF PARAMETER
         S     R9,=F'1'                .DECREMENT 1 FOR MOVE
         BM    OPENR                   .NO WAIT TIME SPECIFIED
*
         LA    R7,2(R2)                .LOAD INPUT ADDRESS
         LA    R8,WTIME                .LOAD WAIT TIME ADDRESS
         EX    R9,MOVEIN               .MOVE INPUT TO WAIT TIME
*
**********************************************************************
*** OPEN INPUT FILE AND TEST
**********************************************************************
OPENR    EQU   *
         LA    R7,INFILE                .LOAD ADDR. OF INPUT DCB
         LA    R8,SYSIN                 .LOAD ADDR. OF WORKSTOR DCB
         LA    R9,LENINP                .LOAD LENGTH OF INPUT DCB
         EX    R9,MOVEIN                .MOVE DCB TO WORKSTOR
         LA    R6,OPENIN                .LOAD DCB ADDRESS
         OPEN  (SYSIN,(INPUT)),MF=(E,(6))   .OPEN INPUT FILE
         LTR   R15,R15                  .INPUT FILE PRESENT ?
         BNZ   FINISH                   .NO INPUT FILE
*
         LA    R7,CMDDUMMY              .LOAD ADDR. DUMMY CMD AREA
         LA    R8,CMDAREA               .LOAD ADDR. OF COMMAND AREA
         LA    R9,CMDLEN                .LOAD LENGTH OF COMMAND AREA
         EX    R9,MOVEIN                .MOVE DUMMY TO COMMAND AREA
*
READIN   EQU   *
         GET   SYSIN,INCMD              .READ INPUT
         MVC   COMMAND(80),INCMD        .READ INPUT
         STIMER WAIT,DINTVL=WTIME      .WAIT THE SPECIFIED TIME
         MODESET KEY=ZERO,MODE=SUP      .SET SUPERVISOR STATE
         XR    R0,R0                    .CLEAR R0 - FOR SVC 34
         MGCR  CMDAREA                  .ISSUE SYSTEM COMMAND
         MODESET KEY=NZERO,MODE=PROB    .SET PROBLEM STATE
         B     READIN                   .READ NEXT COMMAND
*
**********************************************************************
*** DO END OF PROGRAM HOUSEKEEPING
**********************************************************************
CLOSE    EQU   *
         LA    R6,CLOSEIN               .LOAD DCB ADDRESS
         CLOSE SYSIN,MF=(E,(6))         .CLOSE INPUT FILE
FINISH   EQU   *
         XR    R15,R15                  .SET RETURN CODE TO ZERO
         L     R13,4(,R13)              .RESTORE SAVAREA ADDRESS
         ST    R15,8(,R13)              .CLR BACKWARD SAVAREA PNTR
         FREEMAIN R,LV=(10),A=(11)      .FREE STORAGE - WORKSTOR
         LM    R0,R12,20(R13)           .RESTORE REGISTERS
         L     R14,12(R13)              .   DO NOT OVERWRITE R15
         BR    R14                      .END OF EXECUTABLE INSTR.
         EJECT
**********************************************************************
*** LITERAL POOL
**********************************************************************
         LTORG
*
**********************************************************************
*** INSTRUCTION MASKS
**********************************************************************
         DS    0F                      .ALIGN ON FULLWORD BOUNDARY
MOVEIN   MVC   0(0,R8),0(R7)           .MOVE WITH LENGTH
         DS    0F                      .ALIGN ON FULLWORD BOUNDARY
OPENIN   OPEN  (INFILE,(INPUT)),MF=L    .OPEN INPUT FILE LIST
         DS    0F                      .ALIGN ON FULLWORD BOUNDARY
CLOSEIN  CLOSE INFILE,MF=L              .CLOSE INPUT FILE LIST
*
**********************************************************************
*** DUMMY DCB FOR INPUT DATA SET
**********************************************************************
INFILE   DCB   DDNAME=SYSIN,            .INPUT DATA SET                X
               DSORG=PS,                .DCB INFO                      X
               RECFM=FB,                .DCB INFO                      X
               LRECL=80,                .DCB INFO                      X
               EODAD=CLOSE,             .AT END OF INPUT               X
               MACRF=GM                 .USING GET MOVE
LENINP   EQU   *-INFILE
*
**********************************************************************
*** REGISTER EQUATES
**********************************************************************
EQUATES  EQUATES
*
**********************************************************************
*** SYSTEM COMMAND DUMMY AREA
**********************************************************************
CMDDUMMY DS    0F
         DC    X'00'                    .NO TOKEN PRESENT
         DC    AL1(CMDLEN)              .LENGTH OF COMMAND
         DC    X'0000'                  .NO TOKEN PRESENT
         DC    CL80' '                  .MAX LENGTH IS 80 BYTES
CMDLEN   EQU   *-CMDDUMMY
         EJECT
**********************************************************************
*** WORK STORE - DUMMY SECTION
**********************************************************************
WORKSTOR DSECT
**********************************************************************
*** REGISTER SAVE AREA
**********************************************************************
SAVAREA  DS    18F                      .SAVE AREA
WTIME    DS    D                        .WAIT TIME
INCMD    DS    CL80                     .COMMAND READ
*
**********************************************************************
*** WRITE TO OPERATOR MESSAGE AREA
**********************************************************************
         DS    0F                      .ALIGN ON FULLWORD BOUNDARY
CMDAREA  DS    CL(CMDLEN)               .DEFINE COMMAND AREA
         ORG   CMDAREA+4
COMMAND  DS    CL(CMDLEN-4)             .COMMAND
         ORG
*
**********************************************************************
*** DCB FOR INPUT DATA SET
**********************************************************************
SYSIN    DS    CL(LENINP)               .SYSIN FILE DCB
*
LENWORK  EQU   *-WORKSTOR               .LENGTH OF DSECT
         END
