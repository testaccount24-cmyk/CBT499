//SYS0015A JOB (A,RESYS,CIC,00),'CHRIS',
//             CLASS=A,
//             MSGCLASS=V,
//             NOTIFY=SYS0015
//******** *
//ASM      EXEC PGM=IEV90,REGION=4M,PARM='DECK,NOOBJECT,LIST,NOALIGN'
//SYSLIB   DD  DSN=SYS2005.PRDA.LIB.MACLIB2,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSPUNCH DD  DSN=&&LOADSET,UNIT=SYSDA,DISP=(,PASS),
//             SPACE=(400,(100,100,1))
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD *
PTLMAC01 ENTER BASE=(12),AUTH='CHRIS OPPERMAN',DATEW='1996-08-26',     *
               FUNC='SCAN SOURCE LIB FOR DFHXX MACROS'
GETPARM  DS    0H
         L     R2,0(R1)
         LH    R3,0(R2)
         BCTR  R3,0
         LA    R2,2(,R2)
         EX    R3,MOVPARM
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
ALLOCDD  DS    0H
         BAL   R6,DYNALLOC
SCANDDS  DS    0H
         OPEN  (SCANDD)
         LA    R7,MEMTABLE
SCANGET  DS    0H
         GET   SCANDD,DIRREC
         LA    R4,DIRREC
         LH    R5,0(R4)
         LTR   R5,R5
         BZ    CLSSCAN
         LA    R5,0(R4,R5)
         LA    R4,2(,R4)
SCANLOOP DS    0H
         CR    R4,R5
         BNL   SCANGET
         MVC   0(8,R7),0(R4)
SCANNEXT DS    0H
         LA    R4,42(,R4)
         LA    R7,8(,R7)
         B     SCANLOOP
CLSSCAN  DS    0H
         CLOSE SCANDD
         BAL   R6,DEALLOC
OPENPDS  DS    0H
         MVC   DDNPARM(8),=C'PDSINP01'
         MVC   DDNLEN(2),=H'8'
         BAL   R6,DYNALLOC
         OPEN  (PDSINP01,(INPUT))
         OPEN  (REPORT,(OUTPUT))
         MVC   OUTPHDNG+44(44),DSNPARM
         PUT   REPORT,OUTPHDNG
         MVI   OUTPHDNG,C' '
         MVC   OUTPHDNG+1(132),OUTPHDNG
         PUT   REPORT,OUTPHDNG
         LA    R8,MEMTABLE
         ST    R8,R8POINTR
FINDNEXT DS    0H
         CLC   0(8,R8),=X'FFFFFFFFFFFFFFFF'
         BE    EXITRC
         MVC   PDSMEM(8),0(R8)
         BAL   R11,FINDMEM
*        WTO   ' FIND HAPPY'
READBLK  DS    0H
         BAL   R11,READMEM
         BAL   R11,SCANBLK
         B     READBLK
EOFPDS   DS    0H
         LA    R8,8(,R8)
         B     FINDNEXT
* ---------------------------------------------------------------------
EXITRC   DS    0H
         CLOSE (PDSINP01)
         CLOSE (REPORT)
         BAL   R6,DEALLOC
         EXIT  RC=(15)
*
*        DS    F
DYNALLOC DS    0H
         MVI   S99VERB,X'01'
         MVI   S99TXTDS,X'00'
         LA    R1,S99RBPTR
         SVC   99
         LTR   R15,R15
         BZR   R6
DYNERR   DS    0H
         ST    R0,REG0SAVE
         CLC   REG0SAVE(4),=X'17080000'
         BE    DYNNCTLG
         CLC   REG0SAVE(4),=X'17080002'
         BE    DYNNCTLG
         CLC   REG0SAVE(4),=X'02100000'
         BE    DYNBUSY
         B     DYNCRASH
DYNNCTLG DS    0H
*        MVC   OUTDSN(44),LSTDSN
*        MVC   OUTMEM(8),=C'00000000'
*        MVC   OUTDESC(20),=C' NOT CATALOGUED.....'
*        PUT   REPORT,OUTPREC
*        MVC   OUTDESC(20),=C'                    '
*        B     READLST
DYNBUSY  DS    0H
*        MVC   OUTDSN(44),LSTDSN
*        MVC   OUTMEM(8),=C'00000000'
*        MVC   OUTDESC(20),=C' DATASET NOT AVAIL..'
*        PUT   REPORT,OUTPREC
*        MVC   OUTDESC(20),=C'                    '
*        B     READLST
DYNCRASH DS    0H
         DC    H'0'
*
DEALLOC  DS    0H
         MVI   S99VERB,X'02'
         MVI   S99TXTDS,X'80'
         LA    R1,S99RBPTR
         SVC   99
         LTR   R15,R15
         BZR   R6
         DC    H'0'
*
* ------------------------------------------------------------------
FINDMEM  DS    0H
         FIND  PDSINP01,PDSMEM,D
         LTR   R15,R15
         BZR   R11
         WTO   ' START OF MEMBER NOT FOUND.'
         B     EXITRC
* ------------------------------------------------------------------
READMEM  DS    0H
         READ  DECB01,SF,PDSINP01,INAREA,'S'
         CHECK DECB01,DSORG=ALL
*        WTO   ' READ AND CHECH HAPPY'
         BR    R11
* ------------------------------------------------------------------
SCANBLK  DS    0H
         LA    R4,MACTABLE
SCANDATA DS    0H
         LA    R6,INAREA
SCANNXT  DS    0H
         CLC   0(5,R6),=X'1111111111'
         BE    SCANEOD
SCANMAC  DS    0H
         CLC   0(6,R6),0(R4)
         BNE   SCANSTEP
SCANWRIT DS    0H
         MVC   OUTPMEM(8),PDSMEM
         MVC   OUTPMAC(6),0(R6)
         LA    R9,100
SCANTYPE DS    0H
         CLC   0(5,R6),=C'TYPE='
         BE    TYPEFND
         LA    R6,1(,R6)
         BCT   R9,SCANTYPE
         B     SCANSTEP
TYPEFND  DS    0H
         MVC   OUTPTYP(20),0(R6)
         PUT   REPORT,OUTPREC
SCANSTEP DS    0H
         LA    R6,1(,R6)
         B     SCANNXT
SCANEOD  DS    0H
         LA    R4,6(,R4)
         CLC   0(6,R4),=C'DFH000'
         BER   R11
         B     SCANDATA
*
* -------------------------------------------------------------------
MOVPARM  MVC   DSNPARM(0),0(R2)
*
* -------------------------------------------------------------------
*              STORAGE AREAS
* -------------------------------------------------------------------
*
REG0SAVE DC    F'0'
R8POINTR DS    F
*
FRSTTIME DC    CL1'1'
PDSMEM   DC    CL8'TSTMEM  '
OUTPHDNG DS    CL333
         ORG   OUTPHDNG
         DC    CL1'1'
         DC    132CL1' '
         ORG   ,
OUTPREC  DS    CL133
         ORG   OUTPREC
         DC    2CL1' '
OUTPMEM  DC    8CL1' '
         DC    2CL1' '
OUTPMAC  DC    6CL1' '
         DC    2CL1' '
OUTPTYP  DC    20CL1' '
OUTPFIL  DC    93CL1' '
         ORG   ,
*
MACTABLE DC    CL6'DFHDC '
         DC    CL6'DFHDI '
         DC    CL6'DFHFC '
         DC    CL6'DFHIC '
         DC    CL6'DFHJC '
         DC    CL6'DFHKC '
         DC    CL6'DFHPC '
         DC    CL6'DFHSC '
         DC    CL6'DFHSP '
         DC    CL6'DFHTC '
         DC    CL6'DFHTD '
         DC    CL6'DFHTR '
         DC    CL6'DFHTS '
         DC    CL6'DFH000'
         DS    0F
MEMTABLE DC    100CL8'########'         100*(8)   (100 X MEM'S)
MEMTABLN EQU   10*8
DIRREC   DC    256CL1' '
* -------------------------------------------------------------------
*              DYNAMIC ALLOCATION
* -------------------------------------------------------------------
         DS    0F
S99RBPTR DC    X'80'
         DC    AL3(S99RB)
S99RB    DC    X'14'
S99VERB  DC    X'01'
S99FLAG1 DC    X'40'
         DC    X'00'
S99INFO  DC    F'0'
         DC    AL4(S99TXPTR)
         DC    2F'0'
S99TXPTR DC    AL4(DDNKEY)
S99TXTDS DC    AL4(DSNKEY)
         DC    X'80'
         DC    AL3(STAKEY)
*              DDNAME ALLOCATION
DDNKEY   DC    X'0001'
DDNNUM   DC    X'0001'
DDNLEN   DC    X'0006'
DDNPARM  DC    C'SCANDD  '
*              DSN ALLOCATION
DSNKEY   DC    X'0002'
DSNNUM   DC    X'0001'
DSNLEN   DC    X'002C'
DSNPARM  DC    44CL1' '
*              DS-STAT ALLOCATION
STAKEY   DC    X'0004'
STANUM   DC    X'0001'
STALEN   DC    X'0001'
STAPARM  DC    X'08'                   .SHR
         LTORG
* -------------------------------------------------------------------
*              DCB' S
* -------------------------------------------------------------------
SCANDD   DCB   MACRF=(GM),DDNAME=SCANDD,DSORG=PS,RECFM=U,BLKSIZE=256,  *
               EODAD=CLSSCAN
PDSINP01 DCB   MACRF=(R),DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120,      *
               DDNAME=PDSINP01,EODAD=EOFPDS
REPORT   DCB   MACRF=(PM),DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=1330,   *
               DDNAME=PRINTER
*
INAREA   DC    18254XL1'11'
         END
/*
//*
//LKED     EXEC PGM=IEWL,REGION=4M,COND=(4,LT,ASM),
//             PARM='LIST,LET,XREF'
//SYSLIB   DD  DSN=SYS2005.PRDA.LIB.LOAD2,DISP=SHR
//SYSLMOD  DD  DSN=SYS2005.PRDA.LIB.LOAD2,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,DCB=BLKSIZE=1024,SPACE=(1024,(200,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSIN    DD  *
 ENTRY PTLMAC01
 NAME PTLMAC01(R)
/*
//*
//PTLMAC01 EXEC PGM=PTLMAC01,
//    PARM='SYS0015.PRDA.LIB.TEST',COND=(0,NE)
//STEPLIB  DD   DSN=SYS2005.PRDA.LIB.LOAD2,DISP=SHR
//PRINTER  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSUDUMP DD   SYSOUT=*
//SYSIN    DD   DUMMY
