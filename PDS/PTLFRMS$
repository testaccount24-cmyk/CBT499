//SYS0015A JOB (A,RESYS,CIC,00),'CHRIS',
//             CLASS=A,
//             MSGCLASS=V,
//             NOTIFY=SYS0015
//******** *
//ASM      EXEC PGM=IEV90,REGION=4M,PARM='DECK,NOOBJECT,LIST,NOALIGN'
//SYSLIB   DD  DSN=SYS2005.PRDA.LIB.MACLIB2,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSPUNCH DD  DSN=&&LOADSET,UNIT=SYSDA,DISP=(,PASS),
//             SPACE=(400,(100,100,1))
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD *
PTLFRMSC ENTER BASE=(12),AUTH='CHRIS OPPERMAN',DATEW='1996-08-26',     *
               FUNC='REPORT NEXT-FORM CHAIN'
GETPARM  DS    0H
         L     R2,0(R1)
         LH    R3,0(R2)
         LA    R2,2(,R2)
         MVC   BLDLMEM(8),0(R2)
         MVC   STRTMEM(8),0(R2)
OPENIT   DS    0H
         OPEN  (PDSINP01,(INPUT))
         OPEN  (REPORT,(OUTPUT))
         MVC   OUTPREC(18),=C' PTLFRMSC ENTERED.'
         PUT   REPORT,OUTPREC
*
         LA    R8,MEMTABLE
         ST    R8,R8POINTR
STRTPROC DS    0H
         BAL   R11,CHECKMEM
         BAL   R11,FINDMEM
READBLK  DS    0H
         BAL   R11,READMEM
         BAL   R11,SCANBLK
         B     READBLK
*
EOFPDS   DS    0H
         L     R8,R8POINTR
         LR    R9,R8
         LA    R8,180(,R8)
         ST    R8,R8POINTR
WRTMEMTB DS    0H
         MVC   OUTPREC+3(130),0(R9)
         PUT   REPORT,OUTPREC
*        DC    H'0'
         XC    OUTPREC,OUTPREC
*
NEXTONE  DS    0H
         LA    R9,8(,R9)
         MVI   BLDLMEM,C' '
         MVC   BLDLMEM+1(7),BLDLMEM
         LA    R10,BLDLMEM
NEXTMEM  DS    0H
         CLC   0(3,R9),=C'###'
         BE    EXITRC
         CLI   0(R9),C'|'
         BNE   NEXTBLD
         LA    R9,1(,R9)
         CLC   BLDLMEM(8),PREVMEM
         BE    NEXTERR
         CLC   BLDLMEM(8),STRTMEM
         BE    NEXTERR
         MVC   PREVMEM(8),BLDLMEM
         B     STRTPROC
*        BNE   LOADTST
NEXTERR  DS    0H
         MVC   EQMEMMSG+9(8),BLDLMEM
         MVC   EQMEMMSG+27(8),STRTMEM
         WTO   TEXT=(EQMEMMSG,C)
         B     NEXTONE
NEXTBLD  DS    0H
         MVC   0(1,R10),0(R9)
         OI    0(R10),X'40'
         LA    R9,1(,R9)
         LA    R10,1(,R10)
         B     NEXTMEM
EXITRC   DS    0H
         MVC   OUTPREC+3(130),0(R9)
         PUT   REPORT,OUTPREC
         XC    OUTPREC,OUTPREC
*        DC    H'0'
         CLOSE (PDSINP01)
         CLOSE (REPORT)
         EXIT  RC=(15)
* ------------------------------------------------------------------
CHECKMEM DS    0H
         MVC   NXMEMMSG+9(8),BLDLMEM
         XC    OUTPREC,OUTPREC
         MVC   OUTPREC(35),NXMEMMSG
         PUT   REPORT,OUTPREC
         XC    OUTPREC,OUTPREC
*        WTO   TEXT=(NXMEMMSG,C)
         BLDL  PDSINP01,BLDLLST
         LTR   R15,R15
         BZR   R11
         MVC   NOMEMMSG+9(8),BLDLMEM
         WTO   TEXT=(NOMEMMSG,C)
         B     EXITRC
* ------------------------------------------------------------------
FINDMEM  DS    0H
         FIND  PDSINP01,BLDLMEM,D
         LTR   R15,R15
         BZR   R11
         WTO   ' START OF MEMBER NOT FOUND.'
         B     EXITRC
* ------------------------------------------------------------------
SCANBLK  DS    0H
         LH    R4,INAREA                     .LENGTH OF BLOCK
         LA    R6,INAREA
*        GET A SOLUTION FOR 'NO CALL' CONDITION
         MVC   0(8,R8),BLDLMEM
         LA    R8,8(,R8)
SCANNXT  DS    0H
*        CLC   0(11,R6),CALLQRYU
*        BE    MEMBUILD
*        CLC   0(11,R6),CALLQRYL
*        BE    MEMBUILD
         CLC   0(9,R6),NEWFRMUC
         BE    MEMBUILD
         CLC   0(9,R6),NEWFRMLC
         BE    MEMBUILD
         CLC   0(5,R6),CALLTXTU
         BE    MEMBUILD
         CLC   0(5,R6),CALLTXTL
         BE    MEMBUILD
         LA    R6,1(,R6)
         BCT   R4,SCANNXT
         BR    R11
*
MEMBUILD DS    0H
         CLI   0(R6),X'7D'                     .START QUOTE
         BE    MEMFOUND
         LA    R6,1(,R6)
         BCTR  R4,0
         B     MEMBUILD
MEMFOUND DS    0H
         MVI   FOUNDIND,C'1'
         LA    R6,1(,R6)
         BCTR  R4,0
         LR    R7,R6
MEMSCAN  DS    0H
         CLI   0(R7),X'7D'                      .END QUOTE
         BE    MEMEND
         MVC   0(1,R8),0(R7)
         LA    R8,1(,R8)
MEMSTEP  DS    0H
         LA    R7,1(,R7)
         B     MEMSCAN
MEMEND   DS    0H
         MVI   0(R8),C'|'
         LA    R8,1(,R8)
         B     SCANNXT
* ------------------------------------------------------------------
READMEM  DS    0H
         CLI   FOUNDIND,C'1'
         BE    READON
         L     R8,R8POINTR
READON   DS    0H
         MVI   FOUNDIND,C'0'
         READ  DECB01,SF,PDSINP01,INAREA,'S'
         CHECK DECB01,DSORG=ALL
         BR    R11
* -------------------------------------------------------------------
LOADTST  DS    0H
         LOAD  EP=PTLFRMSC,LOADPT=PGMADDR
         MVC   CALLMEM(8),BLDLMEM
         LA    R1,CALLPARM
         L     R15,PGMADDR
*        DC    H'0'
         BALR  R14,R15
         DELETE EP=PTLFRMSC
         B     EXITRC
* -------------------------------------------------------------------
*
* -------------------------------------------------------------------
*              STORAGE AREAS
* -------------------------------------------------------------------
*
BLDLLST  DC    H'1'
         DC    H'18'
BLDLMEM  DC    CL8'TSTMEM  '
         DC    10CL1'1'
*
R8POINTR DS    F
PGMADDR  DS    A
*
         DS    0F
CALLPARM DC    AL4(CALLLEN)
CALLLEN  DC    H'8'
CALLMEM  DS    CL8
*
FOUNDIND DC    CL1'0'
STRTMEM  DC    8CL1' '
PREVMEM  DC    8CL1' '
OUTPREC  DS    CL133
*
CALLTXTU DC    CL5'CALL('
CALLTXTL DC    XL5'838193934D'
NEWFRMUC DC    CL9'NEW_FORM('
NEWFRMLC DC    XL9'9585A66D869699944D'
CALLQRYU DC    CL11'CALL_QUERY('
CALLQRYL DC    XL11'838193936D98A48599A84D'
NOMEMMSG DC    CL35'  MEMBER XXXXXXXX NOT FOUND.       '
NXMEMMSG DC    CL35'  MEMBER XXXXXXXX TO BE FOUND.     '
EQMEMMSG DC    CL35'  MEMBER XXXXXXXX EQ START XXXXXXXX '
MEMTABLE DC    200CL9'#########'      20*9*10 (20X9BYTE COLS,10 ROWS
         LTORG
* -------------------------------------------------------------------
*              DCB' S
* -------------------------------------------------------------------
PDSINP01 DCB   MACRF=(R),DSORG=PO,RECFM=VB,LRECL=250,BLKSIZE=18254,    *
               DDNAME=PDSINP01,EODAD=EOFPDS
REPORT   DCB   MACRF=(PM),DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=1330,   *
               DDNAME=PRINTER
*
INAREA   DC    18254CL1' '
         END
/*
//* ********************
//LKED     EXEC PGM=IEWL,REGION=4M,COND=(4,LT,ASM),
//             PARM='LIST,LET,XREF'
//SYSLIB   DD  DSN=SYS2005.PRDA.LIB.LOAD2,DISP=SHR
//SYSLMOD  DD  DSN=SYS2005.PRDA.LIB.LOAD2,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,DCB=BLKSIZE=1024,SPACE=(1024,(200,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSIN    DD  *
 ENTRY PTLFRMSC
 NAME PTLFRMSC(R)
/*
//*
//PTLFRMSC EXEC PGM=PTLFRMSC,PARM='MAS0135U',COND=(0,NE)
//STEPLIB  DD   DSN=SYS2005.PRDA.LIB.LOAD2,DISP=SHR
//PDSINP01 DD   DSN=SYS0015.TEST.INP,DISP=SHR
//PRINTER  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSUDUMP DD   SYSOUT=*
//SYSIN    DD   DUMMY
