//SYS0011G JOB (A,RESYS,CIC,00),'FDP',
//             CLASS=A,
//             MSGCLASS=V,
//             NOTIFY=SYS0011
//******** *
//ASM      EXEC PGM=ASMA90,REGION=4M,PARM='DECK,NOOBJECT,LIST,NOALIGN'
//SYSLIB   DD  DSN=SYS0011.LIB.ASM,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSPUNCH DD  DSN=&&LOADSET,UNIT=SYSDA,DISP=(,PASS),
//             SPACE=(400,(100,100,1))
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD *
PTLVSCAN ENTER BASE=(12),AUTH='CHRIS OPPERMAN',DATEW='1995-05-12',     *
               FUNC='SCAN FOR SPECIFIED MEMBER'
GETPARM  DS    0H
         L     R2,0(R1)
         LH    R3,0(R2)
         BCTR  R3,0
         LA    R2,2(,R2)
         MVC   BLDLMEM(8),0(R2)
OPENIT   DS    0H
         OPEN  (VTOCLST,(INPUT))
         OPEN  (REPORT,(OUTPUT))
READLST  DS    0H
         GET   VTOCLST,LSTREC
         CLC   LSTREC+1(7),=C'VOLSER='
         BNE   CHKDSORG
         MVC   OUTVOL(6),LSTREC+8
         B     READLST
CHKDSORG DS    0H
         CLC   LSTREC+82(2),=C'PO'
         BNE   READLST
         CLC   LSTREC+1(11),=C'SYS2.JCLLIB'
         BE    READLST
         MVC   LSTDSN(44),LSTREC+1
         MVC   DSNPARM(44),LSTREC+1
ALLOCDD  DS    0H
         BAL   R6,DYNALLOC
SCANDDS  DS    0H
         OPEN  (SCANDD)
SCANGET  DS    0H
         GET   SCANDD,DIRREC
         LA    R4,DIRREC
         LH    R5,0(R4)
         LTR   R5,R5
         BZ    CLSSCAN
         LA    R5,0(R4,R5)
SCANLOOP DS    0H
         CR    R4,R5
         BNL   SCANGET
         LA    R6,2(R4)
         EX    R3,CHECK
*        CLC   2(3,R4),0(R2)
         BNE   SCANNEXT
         MVI   FNDFLAG,C'1'
         MVC   OUTDSN(44),LSTDSN
         MVC   OUTMEM(8),2(R4)
         PUT   REPORT,OUTPREC
SCANNEXT DS    0H
         CLC   LSTREC+88(2),=C'U '
         BNE   SCANSRCE
         LA    R4,36(R4)
         B     SCANLOOP
SCANSRCE DS    0H
         LA    R4,42(R4)
         B     SCANLOOP
CLSSCAN  DS    0H
*        DC    X'0000'
         CLOSE SCANDD
         BAL   R6,DEALLOC
         B     READLST
EOFVTOC  DS    0H
FINIS    DS    0H
         CLI   FNDFLAG,C'1'
         BE    EXITRC
         LA    R15,4
EXITRC   DS    0H
         CLOSE (VTOCLST)
         CLOSE (REPORT)
         EXIT  RC=(15)
*
         DS    F
CHECK    CLC   0(0,R6),0(R2)
DYNALLOC DS    0H
         MVI   S99VERB,X'01'
         MVI   S99TXTDS,X'00'
         LA    R1,S99RBPTR
         SVC   99
         LTR   R15,R15
         BZR   R6
DYNERR   DS    0H
         ST    R0,REG0SAVE
         CLC   REG0SAVE(4),=X'17080000'
         BE    DYNNCTLG
         CLC   REG0SAVE(4),=X'17080002'
         BE    DYNNCTLG
         CLC   REG0SAVE(4),=X'02100000'
         BE    DYNBUSY
         B     DYNCRASH
DYNNCTLG DS    0H
         MVC   OUTDSN(44),LSTDSN
         MVC   OUTMEM(8),=C'00000000'
         MVC   OUTDESC(20),=C' NOT CATALOGUED.....'
*        PUT   REPORT,OUTPREC
         MVC   OUTDESC(20),=C'                    '
         B     READLST
DYNBUSY  DS    0H
         MVC   OUTDSN(44),LSTDSN
         MVC   OUTMEM(8),=C'00000000'
         MVC   OUTDESC(20),=C' DATASET NOT AVAIL..'
         PUT   REPORT,OUTPREC
         MVC   OUTDESC(20),=C'                    '
         B     READLST
DYNCRASH DS    0H
         DC    H'0'
*
DEALLOC  DS    0H
         MVI   S99VERB,X'02'
         MVI   S99TXTDS,X'80'
         LA    R1,S99RBPTR
         SVC   99
         LTR   R15,R15
         BZR   R6
         DC    H'0'
*
* -------------------------------------------------------------------
*              STORAGE AREAS
* -------------------------------------------------------------------
REG0SAVE DC    F'0'
BLDLLST  DC    H'1'
         DC    H'18'
BLDLMEM  DC    CL8'TSTMEM  '
         DC    10CL1'1'
FNDFLAG  DC    CL1'0'
LSTREC   DS    133CL1' '
OUTPREC  DS    CL133
         ORG   OUTPREC
         DC    CL1' '
OUTMEM   DS    CL8
         DC    CL1' '
         DC    CL16' FOUND IN DSN : '
OUTDSN   DS    CL44
         DC    CL13' ON VOLUME : '
OUTVOL   DS    CL6
OUTDESC  DC    44CL1' '
         ORG   ,
LSTDSN   DC    44CL1' '
DIRREC   DC    256CL1' '
         LTORG
* -------------------------------------------------------------------
*              DYNAMIC ALLOCATION
* -------------------------------------------------------------------
         DS    0F
S99RBPTR DC    X'80'
         DC    AL3(S99RB)
S99RB    DC    X'14'
S99VERB  DC    X'01'
S99FLAG1 DC    X'40'
         DC    X'00'
S99INFO  DC    F'0'
         DC    AL4(S99TXPTR)
         DC    2F'0'
S99TXPTR DC    AL4(DDNKEY)
S99TXTDS DC    AL4(DSNKEY)
         DC    X'80'
         DC    AL3(STAKEY)
*              DDNAME ALLOCATION
DDNKEY   DC    X'0001'
DDNNUM   DC    X'0001'
DDNLEN   DC    X'0006'
DDNPARM  DC    C'SCANDD'
*              DSN ALLOCATION
DSNKEY   DC    X'0002'
DSNNUM   DC    X'0001'
DSNLEN   DC    X'002C'
DSNPARM  DC    44CL1' '
*              DS-STAT ALLOCATION
STAKEY   DC    X'0004'
STANUM   DC    X'0001'
STALEN   DC    X'0001'
STAPARM  DC    X'08'                   .SHR
* -------------------------------------------------------------------
*              DCB' S
* -------------------------------------------------------------------
SCANDD   DCB   MACRF=(GM),DDNAME=SCANDD,DSORG=PS,RECFM=U,BLKSIZE=256,  *
               EODAD=CLSSCAN
VTOCLST  DCB   MACRF=(GM),DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=18354,  *
               DDNAME=VTOCLST,EODAD=EOFVTOC
REPORT   DCB   MACRF=(PM),DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=1330,   *
               DDNAME=PRINTER
*
         END
/*
//* ********************
//LKED     EXEC PGM=IEWL,REGION=4M,COND=(4,LT,ASM),
//             PARM='LIST,LET,XREF'
//SYSLIB   DD  DSN=SYS0011.LIB.LOAD,DISP=SHR
//SYSLMOD  DD  DSN=SYS0011.LIB.LOAD,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,DCB=BLKSIZE=1024,SPACE=(1024,(200,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSIN    DD  *
 NAME PTLVSCAN(R)
/*
//
//PTLVSCAN EXEC PGM=PTLVSCAN,PARM='CHR',COND=(0,NE)
//STEPLIB  DD   DSN=SYS0011.LIB.LOAD,DISP=SHR
//*SCANDD   DD   DSN=SYS2005.LIB.JCL,DISP=SHR,
//*         DCB=(BLKSIZE=256,RECFM=U)
//VTOCLST  DD   DSN=SYS2005.@VTOC2.LISTALL,DISP=SHR
//PRINTER  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSUDUMP DD   SYSOUT=*
//SYSIN    DD   DUMMY
