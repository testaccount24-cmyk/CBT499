********        *MACRO TO GET ASCB - CAN BE MOVED TO A SYSLIB LIBRARY
         MACRO
         GETASCB  &NAME,&PAR=@00,&RENTSAV=NO,&USE=NO
.*******       *
.*******       *PS@ASCB1:84-11-22 - OBTAIN A USERS ASCB ADDRESS
.*******       *         NAME     = FIELD CONTAINING JOBNAME/USERID
.*******       *         PAR      = CURRENT PARAGRAPH PREFIX
.*******       *         RENTSAV  = NAME OF WORK SAVE AREA
.*******       *         USE      = USE PARAMETER REGS OPTION
.*******       *  REG 1 IS DESTROYED AND CONTAINS @ ASCB OR 0 IF
.*******       *  NOT FOUND
.*******       * B BEKKER      PERSETEL (PTY LTD)
         LCLB  &USEREGS
         LCLC  &ERS,&EPA,&LBL1
&USEREGS SETB  ('&USE' EQ 'YES')            +SET USE SWITCH
&ERS     SETC  '&RENTSAV'                   +GET KEYWORD VALUE
         AIF   ('&RENTSAV' NE 'NO').SAVE    +DOES HE PROVIDE SA??
         B     &PAR.X&SYSNDX                +BRANCH AROUND SAVE AREA
&PAR.Z&SYSNDX DS  4F                        +SAVE AREA FOR WORKREGS
&PAR.X&SYSNDX EQU   *                       +
&ERS     SETC  '&PAR.Z&SYSNDX'              +SET SAVE AREA NAME
.SAVE    ANOP                               +
&PAR.X   STM   RE,R1,&ERS                   +SAVE WORK REGS
         PUSH  USING                 +
         L     RE,FLCCVT2-PSA(R0,R0) +LOAD CVT ADDRESS
         L     RE,CVTASVT-CVT(R0,RE) +LOAD ASVT ADDRESS
         USING ASVT,RE               +
.*       CLC   ASVTASVT,=C'ASVT'     +HIT??
.*       BNER  R0                    +NO - DIE
         L     R0,ASVTMAXU           +LOAD MAX NO OF ASCBS
&PAR.L&SYSNDX EQU   *
         TM    ASVTENTY,ASVTAVAL     +IS IT IN USE???
         BO    &PAR.R&SYSNDX         +NO - BYPASS
         L     R1,ASVTENTY           +LOAD A(ASCB)
         USING ASCB,R1               +
         CLC   ASCBJBNI,=X'00000000' +IS IT AN INITIATED JOB???
         BE    &PAR.S&SYSNDX         +NO - GO GET OTHER ADDRESS
         L     RF,ASCBJBNI           +GET @ JOBNAME ADDRESS
         B     &PAR.C&SYSNDX         +  AND GO CHECK IT
&PAR.S&SYSNDX EQU   *                +
         L     RF,ASCBJBNS           +GET @ STC/TSU/MOUNT NAME ADDRESS
&PAR.C&SYSNDX EQU   *                +
         CLC   &NAME.(8),0(RF)       +HIT??
         BE    &PAR.F&SYSNDX         +YES - RETURN
&PAR.R&SYSNDX EQU   *                +
         LA    RE,4(,RE)             +ADJUST ASVT ADDRESS
         BCT   R0,&PAR.L&SYSNDX      +REPEAT
         XR    R1,R1                 +INDICATE NOT FOUND
&PAR.F&SYSNDX EQU   *                +
         LM    RE,R0,&ERS            +RESTORE WORK REGS
         POP   USING                 +RESTORE
         MEND                               +
