//SYS2005A JOB (A,RESYS,CIC,00),'CHRIS',
//             CLASS=A,
//             MSGCLASS=V,
//             NOTIFY=SYS2005
//******** *
//ASM      EXEC PGM=IEV90,REGION=4M,PARM='DECK,NOOBJECT,LIST,NOALIGN'
//SYSLIB   DD  DSN=SYS2005.LIB.MACLIB2,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSPUNCH DD  DSN=&&LOADSET,UNIT=SYSDA,DISP=(,PASS),
//             SPACE=(400,(100,100,1))
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD *
PTLCKDSK ENTER BASE=(12),AUTH='CHRIS OPPERMAN',DATEW='1995-05-12',     *
               FUNC='CHECK DASD VOLUME FOR CATALOGED DATASETS'
* --------------------------------------------------------------------
* --     PARM VALUE : 'EXC' - EXCEPTIONS ==> ONLY REPORTS EXCEPTIONS -
* --                  'ALL' - ALL        ==> REPORTS ALL DATASETS    -
* --------------------------------------------------------------------
GETPARM  DS    0H
         L     R2,0(R1)
         LH    R3,0(R2)
         BCTR  R3,0
         LA    R2,2(,R2)
         MVC   PARMTXT(3),0(R2)
GETSYSID DS    0H
         LA    R2,16
         L     R2,0(R2)
         USING CVT,R2
         MVC   HDGSYSID(8),CVTSNAME
GETSTOR  DS    0H
         LA    R0,3000
         GETMAIN R,LV=(R0)
         ST    R1,STORADDR
         LR    R9,R1
         USING VOLAREA,R9
         BAL   R6,INITSTOR
OPENIT   DS    0H
         OPEN  (CATLGLST,(INPUT))
         OPEN  (REPORT,(OUTPUT))
GETVOLS  DS    0H
         BAL   R6,BLDVOL_TAB
         BAL   R6,PRTVOL_TAB
         LA    R8,0                        .LINE COUNTER - OUTPUT
READLST  DS    0H
         BAL   R6,CLRINPT
         GET   CATLGLST,LSTREC
         CLC   LSTREC+34(12),=C'LISTING FROM'
         BNE   CHKDSN
         BAL   R6,CHK_CATNAME
CHKDSN   DS    0H
         CLC   LSTREC+5(7),=C'NONVSAM'
         BE    GETDSN
         CLC   LSTREC+5(7),=C'CLUSTER'
         BE    GETDSN
         B     READLST
GETDSN   DS    0H
         MVC   OUTDSN(44),LSTREC+21
GETVOL   DS    0H
         BAL   R6,CLRINPT
         GET   CATLGLST,LSTREC
         CLC   LSTREC+5(7),=C'IDC3014I'
         BE    CAT_ERR
         CLC   LSTREC+12(6),=C'VOLSER'
         BNE   GETVOL
         MVC   OUTVOL(6),LSTREC+30
         CLC   LSTREC+30,=C'MIGRAT'
         BE    WRITEIT
GETDEV   DS    0H
         CLC   LSTREC+41(7),=C'DEVTYPE'
         BNE   ALLOCDD                         .NO IT MUST BE VSAM
         CLC   LSTREC+56(7),=C'3010200'
         BE    ALLOCDD
         MVC   OUTDESC(27),=C' DATASET CATALOGED ON TAPE.'
         B     WRITEIT
ALLOCDD  DS    0H
         BAL   R6,CHKVOLUM
         BAL   R6,DYNALLOC
WRITEIT  DS    0H
         BAL   R6,CHKHEAD
         CLC   PARMTXT(3),=C'EXC'
         BNE   WRITEALL
         CLC   OUTDESC(10),BLANKS
         BE    READLST
WRITEALL DS    0H
         PUT   REPORT,OUTPREC
         LA    R8,1(R8)
         BAL   R6,CLROUTP
         B     READLST
EOFCATLS DS    0H
EXITRC   DS    0H
         LA    R0,3000
         L     R1,STORADDR
         FREEMAIN R,LV=(R0),A=(R1)
         CLOSE (CATLGLST)
         CLOSE (REPORT)
         EXIT  RC=(15)
*
CHKVOLUM DS    0H
         LA    R4,VOLTAB
NEXTVOL  DS    0H
         CLC   0(6,R4),=C'XXXXXX'
         BE    LASTVOL
         CLC   0(6,R4),OUTVOL
         BER   R6
         LA    R4,6(R4)
         B     NEXTVOL
LASTVOL  DS    0H
         MVC   OUTDESC(20),=C' VOLUME NOT MOUNTED.'
         B     WRITEIT
*
DYNALLOC DS    0H
         CLC   OUTDSN(4),=C'ONS.'
         BE    DYN_ACCESS
         MVC   DSNPARM(44),OUTDSN
         MVI   S99VERB,X'01'
         MVI   S99TXTDS,X'00'
         LA    R1,S99RBPTR
         SVC   99
         LTR   R15,R15
         BNZ   DYNERR
         BAL   R7,DEALLOC
         BR    R6
DYNERR   DS    0H
         ST    R0,REG0SAVE
         CLC   REG0SAVE(4),=X'17080000'
         BE    DYNNCTLG
         CLC   REG0SAVE(4),=X'02100000'
         BE    DYNBUSY
         B     DYNNODSN
DYNNCTLG DS    0H
         MVC   OUTDESC(20),=C' NO ALIAS MASTERCAT.'
         BR    R6
DYNBUSY  DS    0H
         MVC   OUTDESC(32),=C' DATASET IN USE BY ANOTHER USER.'
         BR    R6
DYNNODSN DS    0H
         MVC   OUTDESC(22),=C' DATASET NOT ON VOLUME'
         BR    R6
DYN_ACCESS DS  0H
         MVC   OUTDESC(22),=C' NO ACCESS AUTHORITY. '
         BR    R6
*
DEALLOC  DS    0H
         MVI   S99VERB,X'02'
         MVI   S99TXTDS,X'80'
         LA    R1,S99RBPTR
         SVC   99
         LTR   R15,R15
         BZR   R7
         DC    H'0'
*
CLROUTP  DS    0H
         MVI   OUTDSN,C' '
         MVC   OUTDSN+1(43),OUTDSN
         MVI   OUTVOL,C' '
         MVC   OUTVOL+1(5),OUTVOL
         MVI   OUTDESC,C' '
         MVC   OUTDESC+1(45),OUTDESC
         BR    R6
*
CLRINPT  DS    0H
         MVI   LSTREC,C' '
         MVC   LSTREC+1(136),LSTREC
         BR    R6
*
BLDVOL_TAB DS  0H
         XC    UCBWORK(100),UCBWORK
         LA    R4,VOLTAB
         LA    R5,UCBAREA1
         USING UCBCMSEG,R5
SCANNEXT DS    0H
         UCBSCAN COPY,                                                 *
               WORKAREA=UCBWORK,                                       *
               UCBAREA=UCBAREA1,                                       *
               DYNAMIC=YES,                                            *
               RANGE=ALL,                                              *
               DEVCLASS=DASD
         LTR   R15,R15
         BNZ   UCBSCAN_ERR
         TM    UCBSTAT,UCBONLI
         BZ    SCANNEXT
*        DC    H'0'
         MVC   0(6,R4),UCBVOLI
         LA    R4,6(R4)
         B     SCANNEXT
         BR    R6
UCBSCAN_ERR DS 0H
*        WTO   'UCB SCAN ERROR...................'
         BR    R6
*
PRTVOL_TAB DS  0H
         LA    R4,VOLTAB
PRTNEXT  DS    0H
         CLC   0(6,R4),=C'XXXXXX'
         BER   R6
         MVC   OUTPVOL+1(6),0(R4)
         PUT   REPORT,OUTPVOL
         LA    R4,6(R4)
         B     PRTNEXT
*
CAT_ERR  DS    0H
         MVC   OUTDESC(25),=C' CATALOG ERROR - IDC3014I'
         B     WRITEIT
*
CHK_CATNAME DS 0H
         CLC   HDG_CAT(40),LSTREC+58
         BER   R6
         MVC   HDG_CAT(40),LSTREC+58
         PUT   REPORT,HDGREC
         PUT   REPORT,HDGLINE
         LA    R8,2
         BR    R6
*
CHKHEAD  DS    0H
         C     R8,=F'0'
         BE    PRTHEAD
         C     R8,=F'55'
         BH    PRTHEAD
         BR    R6
PRTHEAD  DS    0H
         PUT   REPORT,HDGREC
         PUT   REPORT,HDGLINE
         LA    R8,2
         BR    R6
*
INITSTOR DS    0H
         LA    R3,500
         LA    R4,VOLTAB
INITLOOP DS    0H
         MVC   0(6,R4),=C'XXXXXX'
         LA    R4,6(R4)
         BCT   R3,INITLOOP
         BR    R6
*
* -------------------------------------------------------------------
*              STORAGE AREAS
* -------------------------------------------------------------------
REG0SAVE DC    F'0'
STORADDR DC    F'0'
BLANKS   DC    10CL1' '
LSTREC   DC    137CL1' '
OUTPVOL  DC    133CL1' '
*
HDGLINE  DS    CL133
         ORG   HDGLINE
         DC    CL1' '
         DC    132CL1'-'
         ORG   ,
HDGREC   DS    CL133
         ORG   HDGREC
         DC    CL1'1'
         DC    60CL1' '
         DC    CL22' REPORT FOR CATALOG : '
HDG_CAT  DS    CL40
         DC    CL1'('
HDGSYSID DS    CL8
         DC    CL1')'
         ORG   ,
*
OUTPREC  DS    CL133
         ORG   OUTPREC
         DC    CL1' '
         DC    CL16' DATASET NAME : '
OUTDSN   DS    CL44
         DC    CL13' ON VOLUME : '
OUTVOL   DS    CL6
OUTDESC  DC    53CL1' '
         ORG   ,
LSTDSN   DC    44CL1' '
DIRREC   DC    256CL1' '
PARMTXT  DS    CL8
UCBWORK  DC    100XL1'00'
UCBAREA1 DC    48CL1' '
         LTORG
* -------------------------------------------------------------------
*              DYNAMIC ALLOCATION
* -------------------------------------------------------------------
         DS    0F
S99RBPTR DC    X'80'
         DC    AL3(S99RB)
S99RB    DC    X'14'
S99VERB  DC    X'01'
S99FLAG1 DC    X'40'
         DC    X'00'
S99INFO  DC    F'0'
         DC    AL4(S99TXPTR)
         DC    2F'0'
S99TXPTR DC    AL4(DDNKEY)
S99TXTDS DC    AL4(DSNKEY)
         DC    X'80'
         DC    AL3(STAKEY)
*              DDNAME ALLOCATION
DDNKEY   DC    X'0001'
DDNNUM   DC    X'0001'
DDNLEN   DC    X'0006'
DDNPARM  DC    C'SCANDD'
*              DSN ALLOCATION
DSNKEY   DC    X'0002'
DSNNUM   DC    X'0001'
DSNLEN   DC    X'002C'
DSNPARM  DC    44CL1' '
*              DS-STAT ALLOCATION
STAKEY   DC    X'0004'
STANUM   DC    X'0001'
STALEN   DC    X'0001'
STAPARM  DC    X'08'                   .SHR
* -------------------------------------------------------------------
*              DCB' S
* -------------------------------------------------------------------
* ANDD   DCB   MACRF=(GM),DDNAME=SCANDD,DSORG=PS,RECFM=U,BLKSIZE=256,  *
               EODAD=CLSSCAN
CATLGLST DCB   MACRF=(GM),DSORG=PS,RECFM=VBA,LRECL=133,BLKSIZE=18354,  *
               DDNAME=CATLGLST,EODAD=EOFCATLS
REPORT   DCB   MACRF=(PM),DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=1330,   *
               DDNAME=PRINTER
         CVT   DSECT=YES
         IEFUCBOB PREFIX=YES,LIST=YES,DEVCLAS=DA
*
VOLAREA  DSECT
VOLTAB   DS    500CL6
*
         END
/*
//*
//LKED     EXEC PGM=IEWL,REGION=4M,COND=(4,LT,ASM),
//             PARM='LIST,LET,XREF'
//SYSLIB   DD  DSN=SYS2005.LIB.LOAD,DISP=SHR
//SYSLMOD  DD  DSN=SYS2005.LIB.LOAD,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,DCB=BLKSIZE=1024,SPACE=(1024,(200,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSIN    DD  *
 ENTRY PTLCKDSK
 NAME  PTLCKDSK(R)
/*
//
//PTLCKDSK EXEC PGM=PTLCKDSK,PARM='EXC',COND=(0,NE)
//STEPLIB  DD   DSN=SYS2005.LIB.LOAD,DISP=SHR
//CATLGLST DD   DSN=SYS2005.LISTCAT.LISTALL,DISP=SHR
//PRINTER  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSUDUMP DD   SYSOUT=*
//SYSIN    DD   DUMMY
