//SYS2005A JOB (A,RESYS,CIC,00),'CHRIS',
//             CLASS=A,
//             MSGCLASS=V,
//             NOTIFY=SYS2005
//******** *
//ASM      EXEC PGM=IEV90,REGION=4M,PARM='DECK,NOOBJECT,LIST,NOALIGN'
//SYSLIB   DD  DSN=SYS2005.LIB.MACLIB2,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(1700,(400,400))
//SYSPUNCH DD  DSN=&&LOADSET,UNIT=SYSDA,DISP=(,PASS),
//             SPACE=(400,(100,100,1))
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD *
PTLCKCAT ENTER BASE=(12),AUTH='CHRIS OPPERMAN',DATEW='1995-09-26',     *
               FUNC='CHECK CATALOG FOR DATASET NAME + VOLUME'
GETPARM  DS    0H
         L     R2,0(R1)
         LH    R3,0(R2)
         BCTR  R3,0
         LA    R2,2(,R2)
         MVC   PARMTXT(8),0(R2)
GETSYSID DS    0H
         LA    R2,16
         L     R2,0(R2)
         USING CVT,R2
         MVC   HDGSYSID(8),CVTSNAME
OPENIT   DS    0H
         OPEN  (VTOCLST,(INPUT))
         OPEN  (REPORT,(OUTPUT))
*
READLST  DS    0H
         GET   VTOCLST,LSTREC
         CLC   LSTREC+1(7),=C'VOLSER='
         BNE   CHKDSN
         MVC   HDGVOL+1(6),LSTREC+8
         PUT   REPORT,OUTPHDG
         MVC   OUTVOLDS(6),LSTREC+8
         B     READLST
CHKDSN   DS    0H
         CLC   LSTREC+1(12),=C'------------'
         BNE   READLST
SETDSN   DS    0H
         GET   VTOCLST,LSTREC
         BAL   R6,CHKRUBB
         CLC   LSTREC+37(18),=C'>>>>> END OF VTOC.'
         BE    READLST
         CLC   LSTREC+38(18),=C'>>>>> END OF VTOC.'
         BE    READLST
         MVC   DSNAME(44),LSTREC+1
*
CHECKCAT DS    0H
         MVC   OUTDSN(44),DSNAME
         LOCATE CATENTRY
         LTR   R15,R15
         BZ    CATLGOK
CATLGNOK DS    0H
         BAL   R6,CHKVALID
         MVC   OUTDESC(32),=C' NOT CATALOGED .... INVESTIGATE.'
         PUT   REPORT,OUTPREC
         BAL   R6,CLROUTP
         B     SETDSN
CATLGOK  DS    0H
         LH    R4,LOCAREA
         LA    R5,LOCAREA+2
PRTREPRT DS    0H
         MVC   OUTVOLCT(6),4(R5)
         CLC   OUTVOLDS(6),OUTVOLCT
         BE    WRITEIT
         MVC   OUTDESC(32),=C' DIFFERENT VOLUME IN CATALOG....'
WRITEIT  DS    0H
         CLC   PARMTXT(3),=C'EXC'
         BNE   WRITEALL
         CLC   OUTDESC(10),BLANK18
         BNE   WRITEALL
         BAL   R6,CLROUTP
         B     SETDSN
WRITEALL DS    0H
         PUT   REPORT,OUTPREC
         BAL   R6,CLROUTP
         LA    R5,12(R5)
         BCT   R4,PRTREPRT
         B     SETDSN
EOFVTOC  DS    0H
EXITRC   DS    0H
         CLOSE (VTOCLST)
         CLOSE (REPORT)
         EXIT  RC=(15)
*
* -------------------------------------------------------------------
*              SUBROUTINES
* -------------------------------------------------------------------
CLROUTP  DS    0H
         MVI   OUTDSN,C' '
         MVC   OUTDSN+1(43),OUTDSN
*        MVI   OUTVOLDS,C' '
*        MVC   OUTVOLDS+1(5),OUTVOLDS
         MVI   OUTVOLCT,C' '
         MVC   OUTVOLCT+1(5),OUTVOLCT
         MVI   OUTDESC,C' '
         MVC   OUTDESC+1(45),OUTDESC
         BR    R6
*
CHKRUBB  DS    0H
         CLC   LSTREC+1(18),BLANK18
         BE    SETDSN
         CLC   LSTREC+1(15),=C'D A T A   S E T'
         BE    SETDSN
         CLC   LSTREC+1(12),=C'------------'
         BE    SETDSN
         BR    R6
*
CHKVALID DS    0H
         CLC   OUTDSN(12),=C'SYS1.VTOCIX.'
         BE    SETDSN
         CLC   OUTDSN(12),=C'SYS1.HASPACE'
         BE    SETDSN
         CLC   OUTDSN(13),=C'SYS1.PAGEDUMP'
         BE    SETDSN
         BR    R6
* -------------------------------------------------------------------
*              STORAGE AREAS
* -------------------------------------------------------------------
CATENTRY CAMLST NAME,DSNAME,,LOCAREA
DSNAME   DC    44CL1' '
LOCAREA  DS    0D
         DS    265CL1
*
REG0SAVE DC    F'0'
BLANK18  DC    18CL1' '
LSTREC   DC    133CL1' '
* -------------------------------------------------------------------
OUTPREC  DS    CL133
         ORG   OUTPREC
         DC    CL1' '
         DC    CL16'DATASET NAME  : '
OUTDSN   DS    CL44
         DC    CL13' ON VOLUME : '
OUTVOLDS DS    CL6
         DC    CL1' '
OUTVOLCT DS    CL6
OUTDESC  DC    46CL1' '
         ORG   ,
*
OUTPHDG  DS    CL133
         ORG   OUTPHDG
         DC    CL1'1'
         DC    58CL1'-'
         DC    CL8' VOLSER='
HDGVOL   DC    8CL1'        '
         DC    CL1'('
HDGSYSID DC    8CL1'        '
         DC    CL1')'
         DC    48CL1'-'
         ORG   ,
* -------------------------------------------------------------------
PARMTXT  DS    CL8
         LTORG
* -------------------------------------------------------------------
*              DCB' S
* -------------------------------------------------------------------
VTOCLST  DCB   MACRF=(GM),DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=18354,  *
               DDNAME=VTOCLST,EODAD=EOFVTOC
REPORT   DCB   MACRF=(PM),DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=1330,   *
               DDNAME=PRINTER
*
         CVT DSECT=YES
         END
/*
//* ********************
//LKED     EXEC PGM=IEWL,REGION=4M,COND=(4,LT,ASM),
//             PARM='LIST,LET,XREF'
//SYSLIB   DD  DSN=SYS2005.LIB.LOAD,DISP=SHR
//SYSLMOD  DD  DSN=SYS2005.LIB.LOAD,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,DCB=BLKSIZE=1024,SPACE=(1024,(200,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSIN    DD  *
 ENTRY PTLCKCAT
 NAME PTLCKCAT(R)
/*
//
//PTLCKCAT EXEC PGM=PTLCKCAT,PARM='CHR',COND=(0,NE)
//STEPLIB  DD   DSN=SYS2005.LIB.LOAD,DISP=SHR
//VTOCLST  DD   DSN=SYS2005.@VTOC.LISTALL,DISP=SHR
//PRINTER  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSUDUMP DD   SYSOUT=*
//SYSIN    DD   DUMMY
