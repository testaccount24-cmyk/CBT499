VTOC@UCB CSECT
VTOC@UCB AMODE 31
VTOC@UCB RMODE ANY
***=================================================================***
***      GET LIST OF ALL ONLINE DASD DEVICES
***=================================================================***
         EDCPRLG BASEREG=((12))         .HOUSEKEEPING FOR C INTERFACE
*
         LM    R03,R04,0(R01)           .SAVE START OF VOLSER TABLE
         USING UCB_BUFF,R03             .MAPPING OF RETURN AREA
         LA    R10,UCB_AREA             .AREA FOR UCB COPY
         USING UCBCMSEG,R10             .COPY OF UCB
         XC    WRK_AREA,WRK_AREA        .CLEAR WORKAREA BEFORE CALL
*
NEXT_UCB EQU   *
         UCBSCAN COPY,                  .OBTAIN COPY OF UCB            +
               WORKAREA=WRK_AREA,       .WORKAREA ADDRESS              +
               UCBAREA=UCB_AREA,        .UCB AREA                      +
               DYNAMIC=YES,             .ALSO HCD DEVICES              +
               RANGE=ALL,               .INCLUDE 4-DIGIT DEVICES       +
               DEVCLASS=DASD            .ONLY DASD
         LTR   R15,R15                  .VALID DASD UCB FOUND ?
         BNZ   END_PROG                 .NO - END OF ROUTINE
         TM    UCBSTAT,UCBONLI          .DEVICE ONLINE ?
         BZ    NEXT_UCB                 .NO, GET NEXT UCB.
         TM    UCBFLA,UCBBOX            .DEVICE BOXED ?
         BNZ   NEXT_UCB                 .YES, GET NEXT UCB.
*
         XC    UCB_BUFF(UCB_DLEN),UCB_BUFF   .CLEAR BUFFER
         MVC   UCB_VOLS,UCBVOLI         .VOLSER
         MVC   UCB_UTYP,UCBUNTYP        .UNIT TYPE 0F-3390, 0E-3380
         MVC   UCB_ADDR,UCBCHAN         .ADDRESS OF DEVICE
         LA    R03,UCB_DLEN(,R03)       .NEXT ENTRY
         B     NEXT_UCB                 .NEXT ENTRY
*
END_PROG EQU   *
         MVI   0(R03),X'FF'             .END OF TABLE INDICATOR
         L     R11,CVTPTR               .GET CVT ADDRESS
         USING CVTMAP,R11               .AND GET ADRESSABILITY
         USING SID_BUFF,R04             .GET ADRESSABILITY
         XC    SID_NULL,SID_NULL        .END OF STRING
         MVC   SID_SID,CVTSNAME         .GET SYSTEM ID
         XR    R15,R15                  .SET RC=00
         EDCEPIL                        .PROGRAM EPILOG FOR C
*
***=================================================================***
***      VARIABLES, CONSTANTS AND UCB
***=================================================================***
         LTORG
         EQUATES1
*
WRK_AREA DS    CL100
         DS    0F
UCB_AREA DS    CL48
         IEFUCBOB PREFIX=YES,LIST=YES,DEVCLAS=DA
*
         EDCDSAD                        .SAVE AREA FOR C INTERFACE
*
UCB_BUFF DSECT                          .UCBPING FOR UCB AREA
UCB_VOLS DS    CL6                      .VOLSER
         DS    CL1                      .....BINARY ZEROES.....
UCB_UTYP DS    XL1                      .UNIT TYPE
UCB_ADDR DS    FL2                      .DEVICE ADDRESS
         DS    0H                       ALIGMENT
UCB_DLEN EQU   *-UCB_BUFF               .LENGTH OF AREA
*
SID_BUFF DSECT                          .UCBPING FOR UCB AREA
SID_SID  DS    CL8                      .SYSTEM ID
SID_NULL DS    CL1                      .....BINARY ZEROES.....
*
         CVT   DSECT=YES,PREFIX=YES,LIST=YES   .FOR SYSTEM ID
         END
