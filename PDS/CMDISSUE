CMDISSUE CSECT
CMDISSUE AMODE 24
CMDISSUE RMODE 24
**********************************************************************
** FUNCTION: THIS PROGRAM ISSUES A SYSTEM COMMAND                   **
**                                                                  **
** INPUT:  ADDRESS OF THE COMMAND TO BE WRITTEN TO THE CONSOLE.     **
**                                                                  **
** OUTPUT: SYSTEM COMMAND                                           **
**                                                                  **
** ATTRIBUTES: RE-ENTRANT                                           **
**             THE PROGRAM SHOULD BE APF AUTHORIZED AND LINKED AC=1 **
**                                                                  **
** REGISTER USAGE:                                                  **
**     R2  - PARAMETER LIST ADRRESS (FROM REGISTER 1)               **
**                                                                  **
**     R7  - EXECUTE MVC - INPUT AREA ADDRESS                       **
**     R8  - EXECUTE MVC - OUTPUT AREA ADDRESS                      **
**     R9  - EXECUTE MVC - LENGTH OF AREA TO BE MOVED               **
**     R10 - LENGTH OF WORK STORE                                   **
**     R11 - WORK STORE AND SAVE AREA BASE ADDRESS                  **
**     R12 - PROGRAM BASE ADDRESS                                   **
**     R13 - SAVEAREA ADDRESS                                       **
**     R14 - RETURN ADDRESS OF CALLING PROGRAM                      **
**     R15 - RETURN CODES                                           **
**                                                                  **
**********************************************************************
         STM   R14,R12,12(R13)          .SAVE REGISTERS
         LR    R12,R15                  .LOAD BASE ADDRESS
         USING CMDISSUE,R12             .SET BASE REGISTER
         L     R2,0(,R1)                .SAVE PARAMETER POINTER
*
         LA    R10,LENWORK              .LOAD LENGTH OF WORKSTOR
         GETMAIN R,LV=(10)              .GET MAIN STORAGE - WORKSTOR
         LR    R11,R1                   .LOAD ADDRESS     - WORKSTOR
         USING WORKSTOR,R11             .SET BASE ADDRESS - WORKSTOR
         ST    R13,4(,R11)              .SAVE AREA POINTER
         ST    R11,8(,R13)              .SAVE AREA POINTER
         LR    R13,R11                  .SAVE AREA ADDRESS IN R13
         EJECT
**********************************************************************
*** READ AND PROCESS COMMAND
**********************************************************************
         LA    R7,CMDDUMMY              .LOAD ADDR. DUMMY CMD AREA
         LA    R8,CMDAREA               .LOAD ADDR. OF COMMAND AREA
         LA    R9,CMDLEN                .LOAD LENGTH OF COMMAND AREA
         EX    R9,MOVEIN                .MOVE DUMMY TO COMMAND AREA
*
         LA    R2,0(,R2)                .GET RID OF HIGH ORDER BIT
         LH    R9,0(,R2)                .LOAD LENGTH OF PARAMETER
         S     R9,=F'1'                 .DECREMENT 1 FOR MOVE
         BM    FINISH                   .NO PARAMETER SPECIFIED
*
         LA    R7,2(R2)                 .LOAD ADDR. INPUT COMMAND
         LA    R8,COMMAND               .LOAD ADDR. OF OUTPUT COMMAND
         EX    R9,MOVEIN                .MOVE TO COMMAND AREA
*
         MODESET KEY=ZERO,MODE=SUP      .SET SUPERVISOR STATE
         XR    R0,R0                    .CLEAR R0 - FOR SVC 34
         MGCR  CMDAREA                  .ISSUE SYSTEM COMMAND
         MODESET KEY=NZERO,MODE=PROB    .SET PROBLEM STATE
*
**********************************************************************
*** DO END OF PROGRAM HOUSEKEEPING
**********************************************************************
FINISH   XR    R15,R15                  .SET RETURN CODE TO ZERO
         L     R13,4(,R13)              .RESTORE SAVAREA ADDRESS
         ST    R15,8(,R13)              .CLR BACKWARD SAVAREA PNTR
         FREEMAIN R,LV=(10),A=(11)      .FREE STORAGE - WORKSTOR
         LM    R0,R12,20(R13)           .RESTORE REGISTERS
         L     R14,12(R13)              .   DO NOT OVERWRITE R15
         BR    R14                      .END OF EXECUTABLE INSTR.
         EJECT
**********************************************************************
*** LITERAL POOL
**********************************************************************
         LTORG
*
**********************************************************************
*** EXECUTE INSTRUCTION MASK
**********************************************************************
         DS    0F                      .ALIGN ON FULLWORD BOUNDARY
MOVEIN   MVC   0(0,R8),0(R7)           .MOVE WITH LENGTH
*
**********************************************************************
*** REGISTER EQUATES
**********************************************************************
EQUATES  EQUATES
*
**********************************************************************
*** SYSTEM COMMAND DUMMY AREA
**********************************************************************
CMDDUMMY DS    0F
         DC    X'00'                    .NO TOKEN PRESENT
         DC    AL1(CMDLEN)              .LENGTH OF COMMAND
         DC    X'0000'                  .NO TOKEN PRESENT
         DC    CL100' '                 .MAX LENGTH IS 100 BYTES
CMDLEN   EQU   *-CMDDUMMY
         EJECT
**********************************************************************
*** WORK STORE - DUMMY SECTION
**********************************************************************
WORKSTOR DSECT
**********************************************************************
*** REGISTER SAVE AREA
**********************************************************************
SAVAREA  DS    18F                      .SAVE AREA
*
**********************************************************************
*** SYSTEM COMMAND AREA
**********************************************************************
CMDAREA  DS    CL(CMDLEN)               .DEFINE COMMAND AREA
         ORG   CMDAREA+4
COMMAND  DS    CL(CMDLEN-4)             .COMMAND
         ORG
*
LENWORK  EQU   *-WORKSTOR               .LENGTH OF DSECT
         END
